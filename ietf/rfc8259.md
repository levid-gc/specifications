# JavaScript 对象标记 (JSON) 数据交换格式

## 目录

* [1 介绍](#1)
  * [1.1 本文档中使用的约定](#1.1)
  * [1.2 JSON 规范](#1.2)
  * [1.3 本次修正介绍](#1.3)
* [2 JSON 语法](#2)
* [3 值](#3)
* [4 对象](#4)
* [5 数组](#5)
* [6 数字](#6)
* [7 字符串](#7)
* [8 字符串和字符问题](#8)
  * [8.1 字符编码](#8.1)
  * [8.2 Unicode 字符](#8.2)
  * [8.3 字符串比较](#8.3)
* [14 引用](#14)
  * [14.1 标准化引用](#14.1)
  * [14.2 资料性引用](#14.2)

# <a name="1" /> 1 介绍

JavaScript 对象标记 (JSON) 是一种用于结构化数据序列化的文本格式。它源自 JavaScript 对象字面量，定义在 ECMAScript 编程语言标准，第三版 [[ECMA-262](#ref-ECMA-262)] 中。

JSON 可以表示四种基元类型（字符串，数字，布尔，和 Null）以及两种结构类型（对象和数组）。

字符串由零个或多个 Unicode 字符序列组成 [[UNICODE](#ref-UNICODE)]。注意这个引用指的是当前最新的 Unicode 版本而不是某个特定的发行版本。意料之中，未来 Unicode 规范的更改将会影响到 JSON 的语法。


对象是无序集合，它由零个或多个 name/value 对组成，名称是字符串类型，值可为字符串，数字，布尔，Null，对象或数组类型。

数组是由零个或多个有序的值组成。

"object" 和 "array" 术语来自于 JavaScript 中的约定。

JSON 的设计目标是成为最小的，可移植的，文本的以及 JavaScript 的子集。

## <a name="1.1" /> 1.1 本文档中使用的约定

本文档中出现的“**必须**”，“**不得**”，“**必要**”，“**应当**”，“**不应**”，“**应该**”，“**不应该**”，“**推荐**”，“**不推荐**”，“**可能**”，以及“**可选**”这些关键字的解释请查阅 [BCP 14](https://tools.ietf.org/html/bcp14) [[RFC2119](https://tools.ietf.org/html/rfc2119)] [[RFC8174](https://tools.ietf.org/html/rfc8174)] 中，当且仅当它们以黑体的形式出现，正如这里所示。

本文档中的语法规则请查阅 [[RFC5234](https://tools.ietf.org/html/rfc5234)]。

## <a name="1.2" /> 1.2 JSON 规范

本文档替换了 [[RFC7159](https://tools.ietf.org/html/rfc7159)]。[[RFC7159](https://tools.ietf.org/html/rfc7159)] 废弃了 [[RFC4627](https://tools.ietf.org/html/rfc4627)]，也就是在当中最初描述了 JSON 并注册了 "application/json" 媒体类型。

JSON 在 [[ECMA-404](#ref-ECMA-404)] 也有描述。

上句中提及的 ECMA-404 是标准的，而并不是说实现者需要参考它来理解本文档，但强调的是，在任何一种规范中“JSON 文本”的定义中都没有不一致的地方。但是，请注意，在这一规范中推荐避免 ECMA-404 中允许的多个实践，这是考虑到最大的互操作性。

两文档中的语法目的都是相同的，虽然使用了不同的描述。如果发现两者之间存在差别，ECMA 和 IETF 将会协作更新这两个文档。

如果在任意一个文档中发现错误，那么也应该检查另一个文档来查看是否存在类似的错误；如果确实存在，那么他应该尽可能地被修复。

未来，如果任意一个文档发生变化，ECMA 和 IETF 都将会协作确保两个文档的更改保持一致。

## <a name="1.3" /> 1.3 本次修正介绍

# <a name="2" /> 2 JSON 语法

JSON 文本是由符号 (token) 序列组成的。符号集中包含六个结构型字符，字符串，数字，以及三个字面量名称。

JSON 文本是一个序列化的值。注意，以前的一些 JSON 规范将 JSON 文本限制为一个对象或一个数组。仅生成对象或数组的实现，其 JSON 文本将是可互操作的，因为所有实现都将接受这些遵循 JSON 文本的内容。

```
JSON-text = ws value ws
```

下面是六个结构型字符：

```
begin-array     = ws %x5B ws  ; [ left square bracket

begin-object    = ws %x7B ws  ; { left curly bracket

end-array       = ws %x5D ws  ; ] right square bracket

end-object      = ws %x7D ws  ; } right curly bracket

name-separator  = ws %x3A ws  ; : colon

value-separator = ws %x2C ws  ; , comma
```

六个结构型字符的左右都允许使用不中的空白符。

```
ws = *(
        %x20 /              ; Space
        %x09 /              ; Horizontal tab
        %x0A /              ; Line feed or New line
        %x0D )              ; Carriage return
```

# <a name="3" /> 3 值

JSON 值**必须**是一个对象，数组，数字，字符串或以下的三个字面量名称：

```
false
null
true
```

字面量名称**必须**为小写形式。不允许使用其他形式的字面量名称。

```
value = false / null / true / object / array / number / string

false = %x66.61.6c.73.65   ; false

null  = %x6e.75.6c.6c      ; null

true  = %x74.72.75.65      ; true
```

# <a name="4" /> 4 对象

对象结构是由一对包围零个或多个 name/value 对（或成员）的花括号表示的。一个名称则是一个字符串。每个名称后面都跟一个分号，用于将值和名称分隔开。逗号用于将一个值和后续的名称分隔开。一个对象中的名称**应该**是唯一的。

```
object = begin-object [ member *( value-separator member ) ]
         end-object

member = string name-separator value
```

对象中属性名称如果都是唯一的情况下，所有的软件实现都将会接收这样的对象并认同 name-value 映射。如果属性名称不唯一，那么接收了这样一个对象的软件行为将会不可预测。许多实现可能仅会报告最后的 name/value 对。其他的实现在解析失败的情况下会报错，而也有一些实现可能会报告所有的 name/value 对，包括名称重复的项。

人们观察到 JSON 解析库的不同之处是，它们是否会对调用软件的对象成员进行排序。那些行为不依赖于成员顺序的实现将是可互操作的，因为它们不会受到这些差异的影响。

# <a name="5" /> 5 数组

数组结构通过方括号包围的零个或多个值（或元素）的形式表示。元素使用逗号分隔开。

```
array = begin-array [ value *( value-separator value ) ] end-array
```

没有对一个数组中值的类型需要相同有要求。

# <a name="6" /> 6 数字

数字的表示与在大多数编程语言中使用的方式类似。一个数字用十进制方式表示。它包含一个整形部分，可能具有可选的减号前缀，后面可能跟着小数部分或者指数部分。不允许在最前面放置 0。

小数部分是一个小数点后面有一个或多个数字。

指数部分以字母 E （大写或小写）开始，后面可能跟着一个加号或者减号。E 和可选的符号后面跟着一个或者多个数字。

数字值不能用以下的语法表示（比如，Infinity 和 NaN），这是不允许的。

```
number        = [ minus ] int [ frac ] [ exp ]

decimal-point = %x2E                            ; .

digit1-9      = %x31-39                         ; 1-9

e             = %x65 / %x45                     ; e E

exp           = e [ minus / plus ] 1*DIGIT

frac          = decimal-point 1*DIGIT

int           = zero / ( digit1-9 *DIGIT )

minus         = %x2D                            ; -

plus          = %x2B                            ; +

zero          = %x30                            ; 0
```

This specification allows implementations to set limits on the range
and precision of numbers accepted.  Since software that implements
IEEE 754 binary64 (double precision) numbers [IEEE754] is generally
available and widely used, good interoperability can be achieved by
implementations that expect no more precision or range than these
provide, in the sense that implementations will approximate JSON
numbers within the expected precision.  A JSON number such as 1E400
or 3.141592653589793238462643383279 may indicate potential
interoperability problems, since it suggests that the software that
created it expects receiving software to have greater capabilities
for numeric magnitude and precision than is widely available.

Note that when such software is used, numbers that are integers and
are in the range [-(2**53)+1, (2**53)-1] are interoperable in the
sense that implementations will agree exactly on their numeric

# <a name="7" /> 7 字符串

The representation of strings is similar to conventions used in the C
family of programming languages.  A string begins and ends with
quotation marks.  All Unicode characters may be placed within the
quotation marks, except for the characters that MUST be escaped:
quotation mark, reverse solidus, and the control characters (U+0000
through U+001F).

Any character may be escaped.  If the character is in the Basic
Multilingual Plane (U+0000 through U+FFFF), then it may be
represented as a six-character sequence: a reverse solidus, followed
by the lowercase letter u, followed by four hexadecimal digits that
encode the character's code point.  The hexadecimal letters A through
F can be uppercase or lowercase.  So, for example, a string
containing only a single reverse solidus character may be represented
as "\u005C".

Alternatively, there are two-character sequence escape
representations of some popular characters.  So, for example, a
string containing only a single reverse solidus character may be
represented more compactly as "\\".

To escape an extended character that is not in the Basic Multilingual
Plane, the character is represented as a 12-character sequence,
encoding the UTF-16 surrogate pair.  So, for example, a string
containing only the G clef character (U+1D11E) may be represented as
"\uD834\uDD1E".

```
string          = quotation-mark *char quotation-mark

char            = unescaped /
                  escape (
                      %x22 /          ; "    quotation mark  U+0022
                      %x5C /          ; \    reverse solidus U+005C
                      %x2F /          ; /    solidus         U+002F
                      %x62 /          ; b    backspace       U+0008
                      %x66 /          ; f    form feed       U+000C
                      %x6E /          ; n    line feed       U+000A
                      %x72 /          ; r    carriage return U+000D
                      %x74 /          ; t    tab             U+0009
                      %x75 4HEXDIG )  ; uXXXX                U+XXXX

escape          = %x5C                ; \

quotation-mark  = %x22                ; "

unescaped       = %x20-21 / %x23-5B / %x5D-10FFFF
```

# <a name="8" /> 8 字符串和字符问题

## <a name="8.1" /> 8.1 字符编码

JSON text exchanged between systems that are not part of a closed
ecosystem MUST be encoded using UTF-8 [RFC3629].

Previous specifications of JSON have not required the use of UTF-8
when transmitting JSON text.  However, the vast majority of JSON-
based software implementations have chosen to use the UTF-8 encoding,
to the extent that it is the only encoding that achieves
interoperability.

Implementations MUST NOT add a byte order mark (U+FEFF) to the
beginning of a networked-transmitted JSON text.  In the interests of
interoperability, implementations that parse JSON texts MAY ignore
the presence of a byte order mark rather than treating it as an
error.

## <a name="8.2" /> 8.2 Unicode 字符

When all the strings represented in a JSON text are composed entirely
of Unicode characters [UNICODE] (however escaped), then that JSON
text is interoperable in the sense that all software implementations
that parse it will agree on the contents of names and of string
values in objects and arrays.

However, the ABNF in this specification allows member names and
string values to contain bit sequences that cannot encode Unicode
characters; for example, "\uDEAD" (a single unpaired UTF-16
surrogate).  Instances of this have been observed, for example, when
a library truncates a UTF-16 string without checking whether the
truncation split a surrogate pair.  The behavior of software that
receives JSON texts containing such values is unpredictable; for
example, implementations might return different values for the length
of a string value or even suffer fatal runtime exceptions.

## <a name="8.3" /> 8.3 字符串比较

Software implementations are typically required to test names of
object members for equality.  Implementations that transform the
textual representation into sequences of Unicode code units and then
perform the comparison numerically, code unit by code unit, are
interoperable in the sense that implementations will agree in all
cases on equality or inequality of two strings.  For example,
implementations that compare strings with escaped characters
unconverted may incorrectly find that "a\\b" and "a\u005Cb" are not
equal.

# <a name="9" /> 9 解析器

A JSON parser transforms a JSON text into another representation.  A
JSON parser MUST accept all texts that conform to the JSON grammar.
A JSON parser MAY accept non-JSON forms or extensions.

An implementation may set limits on the size of texts that it
accepts.  An implementation may set limits on the maximum depth of
nesting.  An implementation may set limits on the range and precision
of numbers.  An implementation may set limits on the length and
character contents of strings.

# <a name="10" /> 10 生成器

A JSON generator produces JSON text.  The resulting text MUST
strictly conform to the JSON grammar.

# <a name="11" /> 11 IANA 考量

The media type for JSON text is application/json.

Type name:  application

Subtype name:  json

Required parameters:  n/a

Optional parameters:  n/a

Encoding considerations:  binary

Security considerations:  See RFC 8259, Section 12

Interoperability considerations:  Described in RFC 8259

Published specification:  RFC 8259

Applications that use this media type:
  JSON has been used to exchange data between applications written
  in all of these programming languages: ActionScript, C, C#,
  Clojure, ColdFusion, Common Lisp, E, Erlang, Go, Java, JavaScript,
  Lua, Objective CAML, Perl, PHP, Python, Rebol, Ruby, Scala, and
  Scheme.

Additional information:
  Magic number(s): n/a
  File extension(s): .json
  Macintosh file type code(s): TEXT

Person & email address to contact for further information:
  IESG
  <iesg@ietf.org>

Intended usage:  COMMON

Restrictions on usage:  none

Author:
  Douglas Crockford
  <douglas@crockford.com>

Change controller:
  IESG
  <iesg@ietf.org>

Note:  No "charset" parameter is defined for this registration.
  Adding one really has no effect on compliant recipients.

# <a name="12" /> 12 安全考量

Generally, there are security issues with scripting languages.  JSON
is a subset of JavaScript but excludes assignment and invocation.

Since JSON's syntax is borrowed from JavaScript, it is possible to
use that language's "eval()" function to parse most JSON texts (but
not all; certain characters such as U+2028 LINE SEPARATOR and U+2029
PARAGRAPH SEPARATOR are legal in JSON but not JavaScript).  This
generally constitutes an unacceptable security risk, since the text
could contain executable code along with data declarations.  The same
consideration applies to the use of eval()-like functions in any
other programming language in which JSON texts conform to that
language's syntax.

# <a name="13" /> 13 示例

This is a JSON object:

  {
    "Image": {
        "Width":  800,
        "Height": 600,
        "Title":  "View from 15th Floor",
        "Thumbnail": {
            "Url":    "http://www.example.com/image/481989943",
            "Height": 125,
            "Width":  100
        },
        "Animated" : false,
        "IDs": [116, 943, 234, 38793]
      }
  }

Its Image member is an object whose Thumbnail member is an object and
whose IDs member is an array of numbers.

This is a JSON array containing two objects:

  [
    {
        "precision": "zip",
        "Latitude":  37.7668,
        "Longitude": -122.3959,
        "Address":   "",
        "City":      "SAN FRANCISCO",
        "State":     "CA",
        "Zip":       "94107",
        "Country":   "US"
    },
    {
        "precision": "zip",
        "Latitude":  37.371991,
        "Longitude": -122.026020,
        "Address":   "",
        "City":      "SUNNYVALE",
        "State":     "CA",
        "Zip":       "94085",
        "Country":   "US"
    }
  ]

Here are three small JSON texts containing only values:

"Hello world!"

42

true

# <a name="14" /> 14 引用

## <a name="14.1" /> 14.1 标准化引用

* <a name="ref-ECMA-404" /> [ECMA-404] Ecma 国际化组织，“JSON 数据交换格式”，ECMA-404 标准，<http://www.ecma-international.org/publications/standards/Ecma-404.htm> 。
* <a name="ref-IEEE754" /> [IEEE754]  IEEE, "IEEE Standard for Floating-Point Arithmetic", IEEE 754.
* <a name="ref-RFC2119" /> [RFC2119]  Bradner, S., "Key words for use in RFCs to Indicate Requirement Levels", BCP 14, RFC 2119, DOI 10.17487/RFC2119, March 1997, <https://www.rfc-editor.org/info/rfc2119>.
* <a name="ref-RFC2119" /> [RFC3629]  Yergeau, F., "UTF-8, a transformation format of ISO 10646", STD 63, RFC 3629, DOI 10.17487/RFC3629, November 2003, <https://www.rfc-editor.org/info/rfc3629>.
* <a name="ref-RFC2119" /> [RFC5234]  Crocker, D., Ed. and P. Overell, "Augmented BNF for Syntax Specifications: ABNF", STD 68, RFC 5234, DOI 10.17487/RFC5234, January 2008, <https://www.rfc-editor.org/info/rfc5234>.
* <a name="ref-RFC2119" /> [RFC8174]  Leiba, B., "Ambiguity of Uppercase vs Lowercase in RFC 2119 Key Words", BCP 14, RFC 8174, DOI 10.17487/RFC8174, May 2017, <https://www.rfc-editor.org/info/rfc8174>.
* <a name="ref-UNICODE" /> [UNICODE] Unicode 联盟，“Unicode 标准”，<http://www.unicode.org/versions/latest/> 。

## <a name="14.2" /> 14.2 资料性引用

* <a name="ref-ECMA-262" /> [ECMA-262] Ecma 国际化组织，“ECMAScript 语言规范”，ECMA-262 标准，第三版，1999 年 11 月，<http://www.ecma-international.org/publications/files/ECMA-ST-ARCH/ECMA-262,%203rd%20edition,%20December%201999.pdf> 。
* <a name="ref-Err3607" /> [Err3607]  RFC Errata, Erratum ID 3607, RFC 4627, <https://www.rfc-editor.org/errata/eid3607>.
* <a name="ref-Err3915" /> [Err3915]  RFC Errata, Erratum ID 3915, RFC 7159, <https://www.rfc-editor.org/errata/eid3915>.
* <a name="ref-Err4264" /> [Err4264]  RFC Errata, Erratum ID 4264, RFC 7159, <https://www.rfc-editor.org/errata/eid4264>.
* <a name="ref-Err4336" /> [Err4336]  RFC Errata, Erratum ID 4336, RFC 7159, <https://www.rfc-editor.org/errata/eid4336>.
* <a name="ref-Err4388" /> [Err4388]  RFC Errata, Erratum ID 4388, RFC 7159, <https://www.rfc-editor.org/errata/eid4388>.
* <a name="ref-Err607" /> [Err607]   RFC Errata, Erratum ID 607, RFC 4627, <https://www.rfc-editor.org/errata/eid607>.
* <a name="ref-RFC4627" /> [RFC4627]  Crockford, D., "The application/json Media Type for JavaScript Object Notation (JSON)", RFC 4627, DOI 10.17487/RFC4627, July 2006, <https://www.rfc-editor.org/info/rfc4627>.
* <a name="ref-RFC7159" /> [RFC7159]  Bray, T., Ed., "The JavaScript Object Notation (JSON) Data Interchange Format", RFC 7159, DOI 10.17487/RFC7159, March 2014, <https://www.rfc-editor.org/info/rfc7159>.

# <a name="appendix-A" /> 附录 A 相对于 RFC 7159 的变更

This section lists changes between this document and the text in
RFC 7159.

* Section 1.2 has been updated to reflect the removal of a JSON specification from ECMA-262, to make ECMA-404 a normative reference, and to explain the particular meaning of "normative".
* Section 1.3 has been updated to reflect errata filed against RFC 7159, not RFC 4627.
* Section 8.1 was changed to require the use of UTF-8 when transmitted over a network.
* Section 12 has been updated to increase the precision of the description of the security risk that follows from using the ECMAScript "eval()" function.
* Section 14.1 has been updated to include ECMA-404 as a normative reference.
* Section 14.2 has been updated to remove ECMA-404, update the version of ECMA-262, and refresh the errata list.

